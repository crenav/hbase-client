import java.text.DateFormat;
import java.text.SimpleDateFormat;
import org.ajoberstar.grgit.*

buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}

Calendar calendar = Calendar.getInstance();
DateFormat dateFormat = new SimpleDateFormat("yyyyMMddHHmmss")
def buildId = dateFormat.format(calendar.getTime())

def grgit = Grgit.open(project.file('.'))
def jobName = System.getenv("JOB_NAME")
def branch = grgit.branch.current.name

version = new ProjectVersion(0, 1, jobName != null ? buildId : "DEV", branch)

class ProjectVersion {
    Integer major
    Integer minor
    String build
    String branch

    ProjectVersion(Integer major, Integer minor, String build, String branch) {
        this.major = major
        this.minor = minor
        this.build = build
        this.branch = branch
    }

    @Override
    String toString() {
        String fullVersion = "$major.$minor"

        if(build) {
            fullVersion += "-$build"
        }
        if (branch != null && !"master".equals(branch)) {
            fullVersion += "-$branch"
        }

        fullVersion
    }
}
